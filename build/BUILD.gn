# Copyright 2025 The Chromium Authors and Alex313031.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/args.gni")
import("//build/config/win/target_ver.gni")

config("compiler_defaults") {
  configs = []
  defines = []
  include_dirs = []
  cflags = []
  cflags_c = []
  cflags_cc = []
  ldflags = []

  if (is_debug) {
    defines += [
      "DEBUG",
      "_DEBUG",
    ]
  } else {
    defines += [
      "NDEBUG",
      "_NDEBUG",
    ]
  }
  if (use_common_controls) {
    defines += [ "USE_COMMCTRLS", ]
  }
  if (use_unicode) {
    cflags += [
      "-finput-charset=UTF-8",
      "-fexec-charset=UTF-8",
    ]
  }
  if (current_os == "win") {
    cflags += [
      "-Wall",
      "-MMD",
      "-MP",
      "-static",
      "-mfpmath=sse",
      "-mfxsr",
      "-msse",
      "-msse2",
    ]
    cflags_c += [
      "-static-libgcc",
      "-std=c$c_std",
    ]
    if (is_legacy_windows && cpp_std == "20") {
      # Windows XP and older can't handle C++20
      cflags_cc += [ "-std=c++17", ]
    } else {
      cflags_cc += [ "-std=c++$cpp_std", ]
    }
    if (!use_llvm) {
      cflags_cc += [ "-static-libstdc++", ]
    }
    ldflags += [
      #"-Lsrc",
      "-static",
    ]
    configs += [ "//build/config/win:windows_config", ]
  } else if (current_os == "linux") {
    cflags += [
      "-fPIC",
      "-pthread",
    ]
    libs = [
      "-lm",
    ]
    ldflags += [
      "-pic",
    ]
    configs += [ ":linux_config", ]
  }
}

config("linux_config") {
  defines = []
  cflags = []
  ldflags = []

  if (use_unicode) {
    defines += [
      "UNICODE",
      "_UNICODE",
    ]
  }

  if (is_debug) {
    cflags += [
      "-Og",
      "-g",
    ]
  } else {
    if (is_official_build) {
      cflags += [
        "-O3",
        "-g0",
        "-s",
      ]
    } else {
      cflags += [
        "-O1",
        "-g",
      ]
    }
  }
}

config("executable_ldconfig") {
  configs = []
  ldflags = []
  if (is_linux) {
    ldflags += [
      "-Wl,-rpath=\$ORIGIN/",
      "-Wl,-rpath-link=",
    ]
  } else if (is_win) {
    if (console_subsystem) {
      #configs += [ "//build/config/win:console_subsystem", ]
    } else {
      #configs += [ "//build/config/win:windowed_subsystem", ]
    }
  } else if (is_mac) {
    ldflags += []
  }
}
