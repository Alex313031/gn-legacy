declare_args() {
  # Whether to compile in debug mode, with symbols and DEBUG defined.
  is_debug = true

  # Whether to enable DCHECKs and extra assertions
  dcheck_enabled = false

  # What C++ standard to use, valid values are 03, 11, 14, 17, and 20, and 23
  # default is C++17
  cpp_std = "default"

  # What C++ standard to use, valid values are 99, 11, 17, and 23
  # default is C11
  c_std = "default"

  # Whether this is a console app. Sets subsystem and defines _CONSOLE.
  # Cannot be true when windowed_subsystem = true
  console_subsystem = false
}

declare_args() {
  # Whether this is an official release build for users. Adds additional compiler optimization.
  is_official_build = !is_debug

  # Whether this is a console app. Sets subsystem and defines _WINDOWS.
  # Cannot be true when console_subsystem = true
  windowed_subsystem = !console_subsystem
}

if (console_subsystem) {
  assert(!windowed_subsystem, "console_subsystem and windowed_subsystem cannot both be true!")
}

if (is_official_build) {
  assert(!is_debug, "is_official_build cannot be true when is_debug = true")
}

if (cpp_std != "03" && cpp_std != "11" && cpp_std != "14" && cpp_std != "17" && cpp_std != "20" && cpp_std != "23") {
 assert(cpp_std == "default", "Invalid C++ standard!")
}

if (c_std != "99" && c_std != "11" && c_std != "17" && c_std != "23") {
 assert(c_std == "default", "Invalid C standard!")
}

if (cpp_std == "default") {
  cpp_std = "17"
}

if (c_std == "default") {
  c_std = "11"
}

declare_args() {
  # Whether to use LLVM compiler instead of GCC/MinGW
  use_llvm = false
}
