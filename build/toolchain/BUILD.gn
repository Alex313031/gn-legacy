# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/args.gni")

toolchain("gcc") {
  tool("cc") {
    depfile = "{{output}}.d"
    command = "gcc -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    command = "g++ -MMD -MF $depfile {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CXX {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("alink") {
    command = "ar rcs {{output}} {{inputs}}"
    description = "AR {{target_output_name}}{{output_extension}}"

    outputs =
        [ "{{target_out_dir}}/{{target_output_name}}{{output_extension}}" ]
    default_output_extension = ".a"
    output_prefix = "lib"
  }

  tool("solink") {
    soname = "{{target_output_name}}{{output_extension}}"  # e.g. "libfoo.so".
    sofile = "{{output_dir}}/$soname"
    rspfile = soname + ".rsp"
    if (is_mac) {
      os_specific_option = "-install_name @executable_path/$sofile"
      rspfile_content = "{{inputs}} {{solibs}} {{libs}}"
    } else {
      os_specific_option = "-Wl,-soname=$soname"
      rspfile_content = "-Wl,--whole-archive {{inputs}} {{solibs}} -Wl,--no-whole-archive {{libs}}"
    }

    command = "g++ -shared {{ldflags}} -o $sofile $os_specific_option @$rspfile"

    description = "SOLINK $soname"

    # Use this for {{output_extension}} expansions unless a target manually
    # overrides it (in which case {{output_extension}} will be what the target
    # specifies).
    default_output_extension = ".so"

    # Use this for {{output_dir}} expansions unless a target manually overrides
    # it (in which case {{output_dir}} will be what the target specifies).
    default_output_dir = "{{root_out_dir}}"

    outputs = [ sofile ]
    link_output = sofile
    depend_output = sofile
    output_prefix = "lib"
  }

  tool("link") {
    outfile = "{{target_output_name}}{{output_extension}}"
    rspfile = "$outfile.rsp"
    if (is_mac) {
      command = "g++ {{ldflags}} -o $outfile @$rspfile {{solibs}} {{libs}}"
    } else {
      command = "g++ {{ldflags}} -o $outfile -Wl,--start-group @$rspfile {{solibs}} -Wl,--end-group {{libs}}"
    }
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    rspfile_content = "{{inputs}}"
    outputs = [ outfile ]
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}

toolchain("llvm-mingw") {
  # Generic compiler output path
  # Use label_name instead of target_output_name since labels will generally
  # have no spaces and will be unique in the directory.
  object_path = "{{target_out_dir}}/{{label_name}}/{{source_name_part}}"

  if (target_cpu == "x86") {
    mingw_prefix = "i686"
    bin_prefix = "i686"
  } else if (target_cpu == "x64") {
    mingw_prefix = "x86_64"
    bin_prefix = "x64"
  } else {
    print("MinGW toolchain can only target x86/x64 for now.")
  }

  if (host_os == "win") {
    if (use_llvm) {
      CC = "$mingw_prefix-w64-mingw32-clang.exe"
      CXX = "$mingw_prefix-w64-mingw32-clang++.exe"
      LD = "$mingw_prefix-w64-mingw32-clang++.exe"
      AR = "$mingw_prefix-w64-mingw32-llvm-ar.exe"
      RC = "$mingw_prefix-w64-mingw32-windres.exe"
    } else {
      CC = "../../tools/mingw/win/$bin_prefix/bin/$mingw_prefix-w64-mingw32-gcc.exe"
      CXX = "../../tools/mingw/win/$bin_prefix/bin/$mingw_prefix-w64-mingw32-g++.exe"
      LD = "../../tools/mingw/win/$bin_prefix/bin/$mingw_prefix-w64-mingw32-g++.exe"
      AR = "../../tools/mingw/win/$bin_prefix/bin/$mingw_prefix-w64-mingw32-ar.exe"
      RC = "../../tools/mingw/win/$bin_prefix/bin/$mingw_prefix-w64-mingw32-windres.exe"
    }
  } else {
    if (use_llvm) {
      CC = "$mingw_prefix-w64-mingw32-clang"
      CXX = "$mingw_prefix-w64-mingw32-clang++"
      LD = "$mingw_prefix-w64-mingw32-clang++"
      AR = "$mingw_prefix-w64-mingw32-llvm-ar"
      RC = "$mingw_prefix-w64-mingw32-windres"
    } else {
      CC = "../../tools/mingw/linux/$bin_prefix/bin/$mingw_prefix-w64-mingw32-gcc"
      CXX = "../../tools/mingw/linux/$bin_prefix/bin/$mingw_prefix-w64-mingw32-g++"
      LD = "../../tools/mingw/linux/$bin_prefix/bin/$mingw_prefix-w64-mingw32-g++"
      AR = "../../tools/mingw/linux/$bin_prefix/bin/$mingw_prefix-w64-mingw32-ar"
      RC = "../../tools/mingw/linux/$bin_prefix/bin/$mingw_prefix-w64-mingw32-windres"
    }
  }

  tool("cc") {
    depfile = "{{output}}.d"
    command = "$CC {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    command = "$CXX {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}} -c {{source}} -o {{output}}"
    depsformat = "gcc"
    description = "CXX {{output}}"
    outputs =
        [ "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o" ]
  }

  tool("alink") {
    libname = "{{target_output_name}}{{output_extension}}"
    libfile = "{{target_out_dir}}/$libname"
    outlibfile = "{{root_out_dir}}/$libname"
    description = "AR {{output}}"
    if (host_os == "win") {
      command = "$AR rcs $libfile {{inputs}}"
    } else {
      command = "$AR rcs $libfile {{inputs}} && cp -af $libfile $outlibfile"
    }

    default_output_extension = ".a"
    output_prefix = "lib"
    outputs = [ libfile, outlibfile ]
  }

  tool("solink") {
    soname = "{{target_output_name}}{{output_extension}}"  # e.g. "libfoo.dll".
    sofile = "{{output_dir}}/$soname"
    libfile = "{{output_dir}}/lib$soname.a"
    #rspfile = soname + ".rsp"
    os_specific_option = "-Wl,-soname=$soname -Wl,--out-implib,$libfile"

    command = "$CXX -shared {{ldflags}} {{inputs}} -o $sofile {{solibs}} {{libs}} $os_specific_option"

    description = "DLL LINK $soname"

    # Use this for {{output_extension}} expansions unless a target manually
    # overrides it (in which case {{output_extension}} will be what the target
    # specifies).
    default_output_extension = ".dll"

    # Use this for {{output_dir}} expansions unless a target manually overrides
    # it (in which case {{output_dir}} will be what the target specifies).
    default_output_dir = "{{root_out_dir}}"

    outputs = [ sofile, libfile ]
    link_output = sofile
    depend_output = sofile
  }

  tool("rc") {
    command = "$RC {{source}} {{defines}} {{include_dirs}} -O coff {{output}}"
    depsformat = "msvc"
    outputs = [ "$object_path.res" ]
    description = "RC {{output}}"
  }

  tool("link") {
    outfile = "{{target_output_name}}{{output_extension}}"
    #rspfile = "$outfile.rsp"
    command = "$LD {{ldflags}} {{inputs}} -o $outfile {{solibs}} {{libs}}"
    description = "LINK $outfile"
    default_output_dir = "{{root_out_dir}}"
    default_output_extension = ".exe"
    #rspfile_content = "{{inputs}}"
    outputs = [ outfile ]
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }

  tool("copy") {
    command = "cp -af {{source}} {{output}}"
    description = "COPY {{source}} {{output}}"
  }
}
